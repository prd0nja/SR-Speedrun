#include sr\sys\_events;
#include sr\utils\_common;

playerConnect()
{
	self endon("connect");
	self endon("disconnect");
	level notify("connected", self);

	self.enable3DWaypoints = true;
	self.enableDeathIcons = true;
	self.classType = undefined;
	self.selectedClass = false;
	self.teamKill = false;
	self.hidden = false;

	self.sr_mode = self speedrun\core\_run::getMode();
	self.sr_way = "normal_0";
	self.run = 0;

	if (game["state"] == "end")
		return;

	if (!self isFirstConnection())
	{
		self.score = self.pers["score"];
		self.kills = self.pers["kills"];
		self.assists = self.pers["assists"];
		self.deaths = self.pers["deaths"];

		self sr\core\_teams::setTeam("allies");
		return;
	}
	self.pers["score"] = 0;
	self.pers["kills"] = 0;
	self.pers["deaths"] = 0;
	self.pers["assists"] = 0;
	self.pers["lifes"] = 0;
	self.pers["headshots"] = 0;
	self.pers["knifes"] = 0;
	self.pers["activator"] = 0;
	self.pers["isDog"] = false;

	self openMenu("main_mp");
	self thread serverDvars();
}

playerDisconnect()
{
	level notify("disconnected", self);
	self cleanUp();
}

playerLastStand(eInflictor, attacker, iDamage, sMeansOfDeath, sWeapon, vDir, sHitLoc, psOffsetTime, deathAnimDuration)
{
	self suicide();
}

playerDamage(eInflictor, eAttacker, iDamage, iDFlags, sMeansOfDeath, sWeapon, vPoint, vDir, sHitLoc, psOffsetTime)
{
	if (self.sessionteam == "spectator" || game["state"] == "end")
		return;

	level notify("player_damage", self, eAttacker, iDamage, iDFlags, sMeansOfDeath, sWeapon, vPoint, vDir, sHitLoc, psOffsetTime);

	if (isPlayer(eAttacker) && !self sameTeam(eAttacker))
		return;
	if (isPlayer(eAttacker) && self sameTeam(eAttacker) && !eAttacker.teamKill)
		return;
	if (isPlayer(eAttacker) && sMeansOfDeath == "MOD_MELEE" && isWallbang(eAttacker, self))
		return;
	if (sMeansOfDeath == "MOD_FALLING" && (self isQ3() || self isCS()))
		return;

	iDFlags |= level.iDFLAGS_NO_KNOCKBACK;

	self doPlayerDamage(eInflictor, eAttacker, iDamage, iDFlags, sMeansOfDeath, sWeapon, vPoint, vDir, sHitLoc, psOffsetTime);
}

playerKilled(eInflictor, attacker, iDamage, sMeansOfDeath, sWeapon, vDir, sHitLoc, psOffsetTime, deathAnimDuration)
{
	self notify("killed_player");
	self notify("death");

	if (self.sessionteam == "spectator" || game["state"] == "end")
		return;

	level notify("player_killed", self, eInflictor, attacker, iDamage, sMeansOfDeath, sWeapon, vDir, sHitLoc, psOffsetTime, deathAnimDuration);

	if (sHitLoc == "head" && sMeansOfDeath != "MOD_MELEE")
		sMeansOfDeath = "MOD_HEAD_SHOT";

	self thread ragdoll(sHitLoc, vDir, sWeapon, eInflictor, sMeansOfDeath, deathAnimDuration);

	if (self isAllies() && self canSpawn())
		self eventSpawn();
}

playerSpawn()
{
	if (game["state"] == "end")
		return;

	level notify("jumper", self);
	self cleanUp();

	self sr\core\_teams::setHealth();
	self detachAll();

	self.pers["weapon"] = self sr\core\_assets::getCustomizeWeapon()["item"];
	self.pers["knife"] = self sr\core\_assets::getCustomizeKnife()["item"];
	self.pers["knife_skin"] = self sr\core\_assets::getCustomizeKnifeSkin()["id"];
	self.pers["character"] = self sr\core\_assets::getCustomizeCharacter()["model"];
	self.pers["glove"] = self sr\core\_assets::getCustomizeGlove()["model"];

	self setModel(self.pers["character"]);
	self setViewModel(self.pers["glove"]);

	self spawnPlayer();

	if (self.settings["player_knife"])
	{
		self giveWeapon(self.pers["knife"], self.pers["knife_skin"]);
		self setSpawnWeapon(self.pers["knife"]);
		self giveMaxAmmo(self.pers["knife"]);
	}
	else
	{
		self giveWeapon(self.pers["knife"], self.pers["knife_skin"]);
		self giveWeapon(self.pers["weapon"]);
		self setSpawnWeapon(self.pers["weapon"]);
		self giveMaxAmmo(self.pers["weapon"]);
	}

	if (isCJ())
	{
		self setActionSlot(4, "weapon", "rpg_mp");
		self giveWeapon("rpg_mp");
		self giveMaxAmmo("rpg_mp");
	}
	if (game["state"] == "readyup")
	{
		self linkTo(level.tempEntity);
		self disableWeapons();
	}
	if (self getStat(988))
		self setClientDvar("cg_thirdperson", 1);

	self speedrun\core\_run::start();

	self notify("spawned_player");
	level notify("player_spawn", self);
}

playerSpectator()
{
	self endon("disconnect");
	self cleanUp();
	level notify("player_spectator", self);
}

serverDvars()
{
	self endon("disconnect");

	wait 3;
	self setClientDvars(
		"ui_3dwaypointtext", "1",
		"ui_deathicontext", "1",
		"cl_maxpackets", 125,
		"rate", 100000,
		"ip", getDvar("net_ip"),
		"port", getDvar("net_port")
	);
	wait 0.05;
	self setClientDvars(
		"show_hud", "true",
		"hud_enable", 1,
		"cg_drawSpectatorMessages", 1,
		"player_sprintTime", 4,
		"ui_hud_hardcore", 1,
		"ui_uav_client", 0
	);
	wait 0.05;
	self setClientDvars(
		"cef_url", fmt("https://sr-speedrun.com/media/%s", level.dvar["seed"])
	);
}
